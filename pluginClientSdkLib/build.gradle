apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'findbugs'

dependencies {
    // support-v4 is optional for backwards compatibility
    // This should be 'provided' scope, however there is a bug with the Android Gradle
    // plugin so instead the dependency is filtered out from the POM
    // https://code.google.com/p/android/issues/detail?id=68316
    compile group:'com.android.support', name:'support-v4', version:']19,)'

    compile group:'net.jcip', name:'jcip-annotations', version:'1.0'
    compile group:'com.android.support', name:'support-annotations', version:'+'
    compile group:'com.twofortyfouram', name:'android-annotation', version:'[1.0.2,3.0['
    compile group:'com.twofortyfouram', name:'android-assertion', version:'[1.0.2,2.0['
    compile group:'com.twofortyfouram', name:'android-plugin-api-for-locale', version:'[1.0.0,2.0['
    compile group:'com.twofortyfouram', name:'android-spackle', version:'[1.0.4,2.0['

    androidTestCompile group:'com.twofortyfouram', name:'android-test', version:'[1.0.4,2.0['
}

android {
    compileSdkVersion Integer.parseInt(ANDROID_COMPILE_SDK_VERSION)
    buildToolsVersion ANDROID_BUILD_TOOLS_VERSION

    resourcePrefix 'com_twofortyfouram_locale_sdk_client_'

    defaultConfig {
        minSdkVersion Integer.parseInt(ANDROID_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(ANDROID_TARGET_SDK_VERSION)
        versionCode Integer.parseInt(ANDROID_VERSION_CODE)
        versionName ANDROID_VERSION_NAME
        consumerProguardFiles 'proguard-consumer.txt'
    }

    buildTypes {
        debug {
            testCoverageEnabled Boolean.parseBoolean(IS_COVERAGE_ENABLED)
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-project.txt'
        }
    }
}

android.libraryVariants.all { variant ->
    task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
        description "Generates Javadoc for $variant.name."
        def androidHome = System.getenv('ANDROID_HOME')
        source = variant.javaCompile.source
        classpath = files(variant.javaCompile.classpath.files) + files("${androidHome}/platforms/${android.compileSdkVersion}/android.jar")
        options.linksOffline('http://developer.android.com/reference/', "${androidHome}/docs/reference")
        options.links('http://jcip.net.s3-website-us-east-1.amazonaws.com/annotations/doc/', 'http://twofortyfouram.github.io/android-annotation/', 'http://twofortyfouram.github.io/android-plugin-api-for-locale/')
        exclude '**/HostPackageUtil.java'
        exclude '**/UiResConstants.java'
    }
}

group = MAVEN_GROUP
archivesBaseName = MAVEN_ARTIFACT_NAME
version = ANDROID_VERSION_NAME

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: MAVEN_URL) {
                authentication(userName: MAVEN_USERNAME, password: MAVEN_PASSWORD)
            }
            pom.project {
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
            }
            // Exclude support.v4, making it an optional dependency
            pom.whenConfigured { pom -> pom.dependencies.removeAll {dep -> dep.groupId == 'com.android.support' && dep.artifactId == 'support-v4' } }
        }
    }
}

// Workaround for naming conflict between maven and android plugins
// http://stackoverflow.com/questions/18559932/gradle-maven-plugin-install-task-does-not-work-with-android-library-project
task installArchives(type: Upload) {
    description "Installs artifacts to mavenLocal."
    repositories.mavenInstaller {
        configuration = configurations.default
        // Exclude support.v4, making it an optional dependency
        pom.whenConfigured { pom -> pom.dependencies.removeAll {dep -> dep.groupId == 'com.android.support' && dep.artifactId == 'support-v4' } }
    }
}

task findbugs(type: FindBugs) {
    ignoreFailures = true
    classes = fileTree('build/intermediates/classes/release/')
    source = fileTree('src/main/java/')
    classpath = files()
    excludeFilter = file("$rootProject.projectDir/tools/findbugs/android-filter.xml")
    effort = 'max'
    reportLevel = 'low'
    reports {
        // Only one report type can be enabled at a time, apparently.
        xml.enabled = false
        html.enabled = true
        html.destination = "$project.buildDir/outputs/reports/findbugs/findbugs.html"
    }
}
